import java.time.LocalDate

plugins {
    id("uk.gov.justice.hmpps.gradle-spring-boot") version "6.0.4"
    id 'java'
    id "au.com.dius.pact" version "4.6.14"
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'

project.sourceCompatibility = JavaVersion.VERSION_21
project.targetCompatibility = JavaVersion.VERSION_21

group = 'uk.gov.justice.probation'

def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agentDeps.transitive = false
}

configurations.all {
    resolutionStrategy.eachDependency {
        DependencyResolveDetails details ->
            if (details.requested.group == 'org.codehaus.groovy') {
                details.useVersion "3.0.2"
                details.because "needed by rest-assured>=4.3"
            }
    }
}

bootJar {
    manifest = jar.manifest
}

jar {
    enabled = false // we use the boot JAR. Including this after Spring boot 2.5.0 caused issues
    manifest {
        attributes(
                'Implementation-Title': rootProject.name, 'Implementation-Version': version
        )
    }
}

ext {
    restAssuredVersion = '5.5.0'
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework:spring-messaging:5.3.24'

    implementation 'io.jsonwebtoken:jjwt:0.12.6'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.3'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:2.1.1'
    implementation 'io.awspring.cloud:spring-cloud-starter-aws-messaging:2.4.4'
    implementation 'com.amazonaws:aws-java-sdk-sts:1.12.543'

    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.543'
    implementation 'org.projectlombok:lombok:1.18.24'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.projectreactor:reactor-spring:1.0.1.RELEASE'

    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'

    testImplementation(platform("org.junit:junit-bom:5.11.0"))
    testRuntimeOnly("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    testImplementation 'io.projectreactor:reactor-test'


    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:3.4.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'

    testImplementation 'org.awaitility:awaitility:4.2.2'

    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "io.rest-assured:json-path:$restAssuredVersion"
    testImplementation "io.rest-assured:xml-path:$restAssuredVersion"
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:3.0.1"
    testImplementation ('org.mockito:mockito-core:5.13.0') {
        testImplementation("net.bytebuddy:byte-buddy:1.15.1")
    }
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation 'commons-io:commons-io:2.11.0'


    testImplementation 'au.com.dius.pact.consumer:junit5:4.3.15'
    testImplementation 'org.apache.httpcomponents:fluent-hc:4.5.13'
}

test {
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
    filter{
        excludeTestsMatching "*IntTest*"
        excludeTestsMatching "*PactTest*"
    }
}

task pactTest(type: Test) {
    useJUnitPlatform()
    filter{
        includeTestsMatching "*PactTest*"
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform ()

    testLogging {
        events "started", "passed", "skipped", "failed", "standardError"
        exceptionFormat "short"
        showStackTraces = true
        showExceptions = true
        showCauses = true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }

    filter {
        includeTestsMatching '*IntTest*'
        excludeTestsMatching "*PactTest*"
        excludeTestsMatching '*WIP*'
    }
}

task copyAgentConfig(type: Copy) {
    from "applicationinsights.json"
    into "$buildDir/libs"
}

pact {
    publish {
        pactBrokerUrl = System.getenv("PACTBROKER_URL")
        pactBrokerUsername = System.getenv("PACTBROKER_AUTH_USERNAME")
        pactBrokerPassword = System.getenv("PACTBROKER_AUTH_PASSWORD")
        pactDirectory = 'build/pacts'
        consumerVersion = System.getenv("PACTCONSUMER_VERSION")
        tags  = (System.getenv("PACTCONSUMER_TAGS") ?: "main").split(",")
    }
}

assemble.dependsOn copyAgent
assemble.dependsOn copyAgentConfig
